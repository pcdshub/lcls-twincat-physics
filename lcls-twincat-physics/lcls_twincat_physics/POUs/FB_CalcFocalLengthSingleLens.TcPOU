<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_CalcFocalLengthSingleLens" Id="{4986d52d-63ec-4366-87a7-ec07b5c7c44d}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_CalcFocalLengthSingleLens EXTENDS FB_Fallible IMPLEMENTS I_CalcFocalLengthSingleLens
VAR
    iCalcXRayDelta : I_CalcXRayDelta; // The x-ray delta, the real part of the index of refraction, unitless value.

    fFocalLength : LREAL; // The focal length of the lens, in meters.
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Calculate" Id="{ccac877d-2b8a-4013-bf1b-351f6a9d80e6}">
      <Declaration><![CDATA[
METHOD Calculate : LREAL
VAR_INPUT
    sElementName : STRING;
    fRadius : LREAL;
END_VAR
VAR
    fDelta : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Initialize focal length and error values.
fFocalLength := 0.0;
ClearError();

fDelta := iCalcXRayDelta.Calculate(sElementName := sElementName);

// Check for an invalid delta input.
IF iCalcXRayDelta.GetError() THEN
    SetError(
        sMsg := CONCAT('The input delta has the following error message: [',
        CONCAT(iCalcXRayDelta.GetErrorMsg(),
        ']'))
    );
    RETURN;
END_IF

IF fDelta = 0.0 THEN
    SetError(
        sMsg := CONCAT('The delta calculated: [',
        CONCAT(LREAL_TO_STRING(fDelta),
        '] must not be 0.'))
    );
    RETURN;
END_IF

IF fRadius <= 0.0 THEN
    SetError(
        sMsg := CONCAT('The radius specified: [',
        CONCAT(LREAL_TO_STRING(fRadius),
        '] must be greater than 0.'))
    );
    RETURN;
END_IF

fFocalLength := fRadius / 2.0 / fDelta;

Calculate := fFocalLength;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{20bb0204-195a-417d-97fe-efa1e33a672d}">
      <Declaration><![CDATA[
METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)

    iCalcXRayDelta : I_CalcXRayDelta; // The x-ray delta, the real part of the index of refraction, unitless value.
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iCalcXRayDelta := iCalcXRayDelta;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>