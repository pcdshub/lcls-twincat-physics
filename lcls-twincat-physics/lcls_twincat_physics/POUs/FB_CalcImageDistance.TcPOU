<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_CalcImageDistance" Id="{85972a9f-836a-4795-bb47-ce7ac4834d74}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_CalcImageDistance EXTENDS FB_Fallible IMPLEMENTS I_CalcImageDistance
VAR
    iConvertUnits : I_ConvertUnitsLREAL;

    fFocalLengthInverse : LREAL;
    fSourceDistanceInverse : LREAL;
    fImageDistanceInverse : LREAL;

    {attribute 'pytmc' := 'pv: ImageDistance'}
    fbImageDistance : FB_QuantityLREALPublic(Units := 'm');
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Calculate" Id="{24dc5037-bbac-4834-9c1a-8a671b6b88cd}">
      <Declaration><![CDATA[
METHOD Calculate : LREAL
VAR_INPUT
    fFocalLength : LREAL; // The focal length, in meters.
    fSourceDistance : LREAL; // The distance, in meters, from the source to the lens.
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Initialize image distance and error values.
fbImageDistance.Val := 0.0;
ClearError();

// Check for an invalid focal length input.
IF fFocalLength = 0.0 THEN
    SetError(
        Msg := CONCAT('The focal length specified: [',
        CONCAT(LREAL_TO_STRING(fFocalLength),
        '] must not be 0.'))
    );
    RETURN;
END_IF

fFocalLengthInverse := 1.0 / fFocalLength;

// Check for an invalid source distance input.
IF fSourceDistance = 0.0 THEN
    SetError(
        Msg := CONCAT('The source distance specified: [',
        CONCAT(LREAL_TO_STRING(fSourceDistance),
        '] must not be 0.'))
    );
    RETURN;
END_IF

fSourceDistanceInverse := 1.0 / fSourceDistance;

fImageDistanceInverse := fFocalLengthInverse - fSourceDistanceInverse;

// Check for an invalid inverted image distance.
IF fImageDistanceInverse = 0.0 THEN
    SetError(
        Msg := CONCAT('The calculated inverse image distance: [',
        CONCAT(LREAL_TO_STRING(fImageDistanceInverse),
        '] must not be 0.'))
    );
    RETURN;
END_IF

fbImageDistance.Val := 1.0 / fImageDistanceInverse;

Calculate := fbImageDistance.Val;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ClearError" Id="{b94b43b7-c9f4-4520-98ef-fd76bb56d34f}">
      <Declaration><![CDATA[
METHOD PROTECTED ClearError
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
SUPER^.ClearError();

fbImageDistance.ClearError();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{d3c982f5-6b5c-429c-959b-98e8306b08c3}">
      <Declaration><![CDATA[
METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)

    iConvertUnits : I_ConvertUnitsLREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iConvertUnits := iConvertUnits;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetError" Id="{05080ecf-080b-46a2-b697-01f57b259296}">
      <Declaration><![CDATA[
METHOD PROTECTED SetError
VAR_INPUT
    Msg : T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
SUPER^.SetError(Msg := Msg);

fbImageDistance.SetError(Msg := Msg);
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>