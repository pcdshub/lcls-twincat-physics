<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_LensSet" Id="{9055d704-4d25-40c5-873f-804fc0b293d8}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_LensSet EXTENDS FB_Fallible IMPLEMENTS I_LensSet
VAR
    iCalcFocalLengthSingleLens : I_CalcFocalLengthSingleLens;

    afbLens : ARRAY [1..GVL_Physics.nMaxNumLensesInSet] OF FB_Lens;
    nNumLenses : INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{fe559d94-9129-430f-9008-66737ede8a73}">
      <Declaration><![CDATA[
METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)

    iCalcFocalLengthSingleLens : I_CalcFocalLengthSingleLens;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iCalcFocalLengthSingleLens := iCalcFocalLengthSingleLens;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetFocalLengthOf" Id="{1c9b1209-03ff-43db-86e9-e0c5cdd2a72e}">
      <Declaration><![CDATA[
METHOD GetFocalLengthOf : LREAL
VAR_INPUT
    nLensNum	: INT;
END_VAR
VAR
    fFocalLength : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

fFocalLength := iCalcFocalLengthSingleLens.Calculate(iLens := afbLens[nLensNum]);

IF iCalcFocalLengthSingleLens.GetError() THEN
    SetError(sMsg :=
        CONCAT('Error returned when attempting the calculate focal length for lens number: [',
        CONCAT(INT_TO_STRING(nLensNum),
        CONCAT(']. Error message is: ',
        iCalcFocalLengthSingleLens.GetErrorMsg())))
    );
    RETURN;
END_IF

GetFocalLengthOf := fFocalLength;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetMaterialNameOf" Id="{51876747-123d-4353-83f5-0881279e54a8}">
      <Declaration><![CDATA[
METHOD GetMaterialNameOf : T_MaxString
VAR_INPUT
    nLensNum	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

GetMaterialNameOf := afbLens[nLensNum].MaterialName;]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetMultiplierOf" Id="{cf92e14b-e5e6-4f30-bfb0-2bd8a1aa55dd}">
      <Declaration><![CDATA[
METHOD GetMultiplierOf : INT
VAR_INPUT
    nLensNum	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

GetMultiplierOf := afbLens[nLensNum].Multiplier;]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetRadiusOf" Id="{7215f3a4-5170-4bdc-8ce5-3b2710bc63a0}">
      <Declaration><![CDATA[
METHOD GetRadiusOf : LREAL
VAR_INPUT
    nLensNum	: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

GetRadiusOf := afbLens[nLensNum].Radius;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IndexInRange" Id="{736b6ca6-387b-4727-80f6-b6d61a70ddd3}">
      <Declaration><![CDATA[
METHOD PUBLIC IndexInRange : BOOL
VAR_INPUT
    nIndex : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IndexInRange := nIndex > 0 AND nIndex <= NumLenses;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IndexOutOfRangeErrorMsg" Id="{b2d5c30b-5f25-4b92-8eb7-0371b1cc775a}">
      <Declaration><![CDATA[
METHOD IndexOutOfRangeErrorMsg : T_MaxString
VAR_INPUT
    nLensNum : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IndexOutOfRangeErrorMsg :=
        CONCAT('Requested index is out of the allowable range. ',
        CONCAT('0 < [',
        CONCAT(INT_TO_STRING(nLensNum),
        CONCAT('] <= ',
        CONCAT(INT_TO_STRING(NumLenses),
        ' is not true.')))));]]></ST>
      </Implementation>
    </Method>
    <Property Name="NumLenses" Id="{1ababeff-88b4-4598-8f6f-5898604237f0}">
      <Declaration><![CDATA[
PROPERTY NumLenses : INT
]]></Declaration>
      <Get Name="Get" Id="{a045bb85-1352-478d-b38c-0658422571a9}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
NumLenses := nNumLenses;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{79ab4a51-2bc8-4a47-ab7e-f9e6958d9160}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
IF NumLenses > 0 AND NumLenses <= GVL_Physics.nMaxNumLensesInSet THEN
    nNumLenses := NumLenses;
ELSE
    SetError(sMsg := CONCAT('Attempted to set number of lenses to: [',
        CONCAT(INT_TO_STRING(NumLenses),
        CONCAT('] which does not meet the criteria: 0 < NumLenses <= GVL_Physics.nMaxNumLensesInSet = [',
        CONCAT(INT_TO_STRING(GVL_Physics.nMaxNumLensesInSet),
        ']'))))
    );
END_IF
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="SetMaterialNameOf" Id="{fa7f7c0a-b699-4773-9a65-4e6a96415d7f}">
      <Declaration><![CDATA[
METHOD SetMaterialNameOf
VAR_INPUT
    nLensNum	: INT;
    sMaterialName : T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

afbLens[nLensNum].MaterialName := sMaterialName;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetMultiplierOf" Id="{64026241-a25f-4372-9868-f8b9e830a9e6}">
      <Declaration><![CDATA[
METHOD SetMultiplierOf
VAR_INPUT
    nLensNum	: INT;
    nMultiplier : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

afbLens[nLensNum].Multiplier := nMultiplier;

IF afbLens[nLensNum].GetError() THEN
    SetError(sMsg := CONCAT('There was an error when trying to set the multiplier of lens [',
        CONCAT(INT_TO_STRING(nLensNum),
        CONCAT(']: ',
        afbLens[nLensNum].GetErrorMsg())))
    );
    RETURN;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetRadiusOf" Id="{597de808-0ace-440d-99fd-44c3824ba2f2}">
      <Declaration><![CDATA[
METHOD SetRadiusOf
VAR_INPUT
    nLensNum : INT;
    fRadius : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

IF NOT IndexInRange(nLensNum) THEN
    SetError(sMsg := IndexOutOfRangeErrorMsg(nLensNum := nLensNum));
    RETURN;
END_IF

afbLens[nLensNum].Radius := fRadius;

IF afbLens[nLensNum].GetError() THEN
    SetError(sMsg := CONCAT('There was an error when trying to set the radius of lens [',
        CONCAT(INT_TO_STRING(nLensNum),
        CONCAT(']: ',
        afbLens[nLensNum].GetErrorMsg())))
    );
    RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>