<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Lens" Id="{957df398-fc17-4d3d-8259-efbd2826ce0a}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_Lens EXTENDS FB_Fallible IMPLEMENTS I_Lens
VAR
    iConvertUnits : I_ConvertUnitsLREAL;

    {attribute 'pytmc' := '
        pv: MaterialName
        io: io
        field: DESC Name of lens material.
    '}
    sMaterialName : T_MaxString;

    {attribute 'pytmc' := '
        pv: Multiplier
        io: io
        field: DESC Number of lenses of same kind.
    '}
    nMultiplier : INT;

    {attribute 'pytmc' := 'pv: Radius'}
    fbRadius : FB_QuantityLREALPublic(Units := 'm');
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{4497aa12-2471-47f4-b812-c441978fbf9f}">
      <Declaration><![CDATA[
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)

    iConvertUnits : I_ConvertUnitsLREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iConvertUnits := iConvertUnits;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetRadius" Id="{be4de050-0497-4c6f-93c9-3795433ab058}">
      <Declaration><![CDATA[
METHOD GetRadius
VAR_OUTPUT
    Radius : FB_QuantityLREALPublic(Units := '');
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
Radius := fbRadius;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="MaterialFormula" Id="{b2fab391-7651-45c1-ab26-07c187736073}">
      <Declaration><![CDATA[
PROPERTY MaterialFormula : T_MaxString
]]></Declaration>
      <Get Name="Get" Id="{e00aa37c-ca86-4eca-9fcd-e966cf75e9b6}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
MaterialFormula := sMaterialName;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{55ffced3-a939-497c-94f4-6d70d9a46315}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
sMaterialName := MaterialFormula;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="Multiplier" Id="{200a7dfc-f3f3-4666-adbd-e1ca5ac12dd0}">
      <Declaration><![CDATA[
PROPERTY Multiplier : INT
]]></Declaration>
      <Get Name="Get" Id="{b3d82859-9ccf-4e6f-8b3d-7bb3d15ef54d}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
Multiplier := nMultiplier;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{6dc3a0c5-5cdb-435a-8688-d597fbad8625}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[
ClearError();

IF Multiplier <= 0 THEN
    SetError(Msg := NonPositiveMultiplierErrorMsg(
        nMultiplier := Multiplier
    ));
    RETURN;
END_IF

nMultiplier := Multiplier;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="NonPositiveMultiplierErrorMsg" Id="{36a5c803-9bc8-4d9b-91e4-bff88830b366}">
      <Declaration><![CDATA[
METHOD NonPositiveMultiplierErrorMsg : T_MaxString
VAR_INPUT
    nMultiplier : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
NonPositiveMultiplierErrorMsg :=
        CONCAT('Attempted to set muliplier to a non-positive value of: [',
        CONCAT(INT_TO_STRING(nMultiplier),
        ']'));]]></ST>
      </Implementation>
    </Method>
    <Method Name="NonPositiveRadiusErrorMsg" Id="{95415334-fe57-431a-962c-0f4016f5c927}">
      <Declaration><![CDATA[
METHOD NonPositiveRadiusErrorMsg : T_MaxString
VAR_INPUT
    fRadius: LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
NonPositiveRadiusErrorMsg :=
        CONCAT('Attempted to set radius to a non-positive value of: [',
        CONCAT(LREAL_TO_STRING(fRadius),
        ']'));]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetRadius" Id="{0207b0c5-3489-47ba-9113-36b433558829}">
      <Declaration><![CDATA[
METHOD SetRadius
VAR_INPUT
    Radius : I_QuantityLREAL;
END_VAR
VAR
    fbRadiusTemp : FB_QuantityLREALPublic(Units := 'm');
    sUnits : T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
ClearError();

fbRadius.ClearError();

iConvertUnits.ConvertQuantityLREAL(
    QuantityToConvert := Radius,
    DesiredUnits := fbRadius.Units,
    ConvertedQuantity => fbRadiusTemp
);

IF iConvertUnits.GetError() THEN
    SetError(Msg :=
        CONCAT('Error returned when attempting to convert units of radius input: ',
        iConvertUnits.GetErrorMsg())
    );
    fbRadius.SetError(Msg := GetErrorMsg());
    RETURN;
END_IF

IF Radius.Val <= 0 THEN
    SetError(Msg := NonPositiveRadiusErrorMsg(fRadius := Radius.Val));
    fbRadius.SetError(Msg := GetErrorMsg());
    RETURN;
END_IF

fbRadius := fbRadiusTemp;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetRadiusUnits" Id="{3f703e38-66c1-490d-ab71-6b1976f1420b}">
      <Declaration><![CDATA[
METHOD SetRadiusUnits
VAR_INPUT
    Units : T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
fbRadius.Units := Units;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>